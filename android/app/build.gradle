plugins {
     id 'com.android.application'
     id 'kotlin-android'
}

def buildWithCMake = project.hasProperty('BUILD_WITH_CMAKE');

android {
    namespace "io.github.kichikuou.system3"
    compileSdkVersion 35
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 17
        versionName "1.6.1" + gitRevision()
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_APP_PLATFORM=android-19", "-DANDROID_STL=c++_static"
                // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    signingConfigs {
        release {
            storeFile rootProject.file('keystore.jks')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (rootProject.file('keystore.jks').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
            assets.srcDir '../../resources'
        }
        aaptOptions {
            // Disable asset compression for fonts.
            // Without this, text rendering will be very slow.
            noCompress 'ttf', 'otf'
        }
        externalNativeBuild {
            cmake {
                version '3.22.1'
                path 'jni/CMakeLists.txt'
            }
        }

    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
}

static String gitRevision() {
    ' (git ' + "git rev-parse --short HEAD".execute().text.trim() + ')'
}
